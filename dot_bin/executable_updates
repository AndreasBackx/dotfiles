#!/usr/bin/env zsh

set -e

source ~/.bin/utils.zsh

function has_dnf5() {
    type "dnf5" >/dev/null
}

function the_dnf() {
    if has_dnf5; then
        dnf5 $@
    else
        dnf $@
    fi
}

function dnf_update_amount() {
    the_dnf check-update --quiet --disablerepo="cpe-yum*" $@ |
        # Make sure we only get the first part as that's the updates,
        # the rest is less accurate. This makes it take the first paragraph.
        sed "s/Obsoleting Packages//g" |
        awk -v RS= 'NR==1' |
        # Count
        wc -l
}

function check_cache() {
    if is_fedora; then
        amount=$(dnf_update_amount --cacheonly)
    else
        amount=$(paru -Qu | wc -l)
    fi

    # Return as fast as possible, then update the repos and update.
    if is_wayland; then
        waybar-json "$amount"
    else
        echo "$amount"
    fi
}

function check_live() {
    if is_fedora; then
        amount=$(dnf_update_amount || echo "-1")
    else
        amount=$(
            paru -Syy &>/dev/null
            paru -Qu | wc -l || echo "-1"
        )
    fi

    if [[ "$amount" == "-1" ]]; then
        return
    fi

    if is_wayland; then
        waybar-json "$amount"
    else
        echo "$amount"
    fi
}

function update_kitty() {
    kitty @ goto-layout vertical
    kitty @ launch --title "Flatpak update" --no-response --dont-take-focus --type window -- updates update --flatpak
    kitty @ launch --title "Snap update" --no-response --dont-take-focus --type window -- updates update --snap
    kitty @ launch --title "Package manager update" --no-response --dont-take-focus --type window -- updates update --package-manager
}

function update_package_manager() {
    if is_fedora; then
        # We have to inline the_dnf because of sudo.
        if has_dnf5; then
            extra_args=""
            if [[ "$CHEZMOI_DATA_ENVIRONMENT" == "work" ]]; then
                extra_args="--disablerepo=cpe-yum*"
            fi
            sudo dnf5 $@ upgrade --refresh --assumeyes $extra_args
        else
            sudo dnf $@ upgrade --refresh --assumeyes --disablerepo="cpe-yum*"
        fi
    else
        paru -Syu --noconfirm
    fi
}

function update_flatpak() {
    flatpak remove --unused --assumeyes && flatpak update --assumeyes
}

function update_snap() {
    snap &>/dev/null && sudo snap refresh
}

case "$1" in
check)
    check_cache
    check_live
    ;;
monitor)
    check_cache
    while true; do
        check_live
        sleep 60
    done
    ;;
update)
    case "$2" in
    --kitty)
        update_kitty
        ;;
    --package-manager)
        update_package_manager
        ;;
    --flatpak)
        update_flatpak
        ;;
    --snap)
        update_snap
        ;;
    *)
        # If desktop environment is running.
        if [[ -n "$DESKTOP_SESSION" ]]; then
            if [[ "$TERM" == "xterm-kitty" ]]; then
                update_kitty
            else
                kitty --class updates --detach updates update --kitty
            fi
        else
            update_package_manager
            update_flatpak
            update_snap
        fi
        ;;
    esac
    ;;
*)
    echo "Usage: $0 {check|monitor|update}"
    exit 2
    ;;
esac

exit 0
